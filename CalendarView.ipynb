{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Calendar View\n",
    "\n",
    "Given a department code and a term, creates a calendar view of scheduled courses in Banner for the purpose of visualizing course scheduling conflicts.\n",
    "\n",
    "The primary use case for this is determining where to schedule a new IIS class so as to minimize conflicts with other departments, but it would probably be equally useful for a department to visualize its own schedule.\n",
    "\n",
    "Note that you will need to [install and reference NuGet packages](https://docs.microsoft.com/en-us/nuget/tools/nuget-exe-cli-reference) as appropriate on your system.\n",
    "\n",
    "## Common code\n",
    "\n",
    "The code below describes:\n",
    "\n",
    "- A basic Banner POST for all courses offered by a department \n",
    "- A basic Banner POST for a specific class\n",
    "- XPath oriented methods for processing the html (from HtmlAgilityPack)\n",
    "- Html autorepair methods for fixing bad html that breaks XPath (from AngleSharp)\n",
    "- General functions for extracting calendar data and ICS format\n",
    "\n",
    "Since AngleSharp does not support XPath, we use two libraries for html manipulation.\n",
    "Refactoring to remove XPath/HtmlAgilityPack would probably be sensible at some point."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 33,
   "metadata": {},
   "outputs": [],
   "source": [
    "#r \"/z/aolney/repos/FSharp.Data.2.3.2/lib/net40/FSharp.Data.dll\"\n",
    "#r \"/z/aolney/repos/HtmlAgilityPack.1.4.9.5/lib/Net40/HtmlAgilityPack.dll\"\n",
    "#r \"/z/aolney/repos/AngleSharp.0.9.10/lib/net45/AngleSharp.dll\"\n",
    "\n",
    "open FSharp.Data\n",
    "open HtmlAgilityPack\n",
    "open System\n",
    "\n",
    "let QueryClass (dept:string) (number:string) (term:string) =\n",
    "    let result = \n",
    "        Http.RequestString( \n",
    "            \"https://banssbprod.memphis.edu/pls/PROD/bwckschd.p_get_crse_unsec\",\n",
    "            query=[\n",
    "                \"term_in\", term.Trim();\n",
    "                \"sel_subj\",\"dummy\";\n",
    "                \"sel_day\",\"dummy\";\n",
    "                \"sel_schd\",\"dummy\";\n",
    "                \"sel_insm\",\"dummy\";\n",
    "                \"sel_camp\",\"dummy\";\n",
    "                \"sel_levl\",\"dummy\";\n",
    "                \"sel_sess\",\"dummy\";\n",
    "                \"sel_instr\",\"dummy\";\n",
    "                \"sel_ptrm\",\"dummy\";\n",
    "                \"sel_attr\",\"dummy\";\n",
    "                \"sel_subj\",dept.Trim();\n",
    "                \"sel_crse\",number.Trim();\n",
    "                \"sel_title\",\"\";\n",
    "                \"sel_insm\",\"%\";\n",
    "                \"sel_from_cred\",\"\";\n",
    "                \"sel_to_cred\",\"\";\n",
    "                \"sel_camp\",\"%\";\n",
    "                \"sel_levl\",\"%\";\n",
    "                \"sel_ptrm\",\"%\";\n",
    "                \"sel_instr\",\"%\";\n",
    "                \"sel_attr\",\"%\";\n",
    "                \"begin_hh\",\"0\";\n",
    "                \"begin_mi\",\"0\";\n",
    "                \"begin_ap\",\"a\";\n",
    "                \"end_hh\",\"0\";\n",
    "                \"end_mi\",\"0\";\n",
    "                \"end_ap\",\"a\" ], \n",
    "            httpMethod=\"POST\")\n",
    "    let classFound = result.Contains(\"No classes were found that meet your search criteria\") |> not\n",
    "    //\n",
    "    classFound,result\n",
    "\n",
    "let QueryDepartment (dept:string) (term:string) =\n",
    "    QueryClass dept \"\" term |> snd\n",
    "\n",
    "//Creates a doc from html string\n",
    "let createDoc html =\n",
    "    let doc = new HtmlDocument()\n",
    "    doc.LoadHtml html\n",
    "    doc.DocumentNode\n",
    "   \n",
    "let descendants (name : string) (node : HtmlNode) = \n",
    "    node.Descendants name\n",
    "    \n",
    "let selectNodes (query : string) (node : HtmlNode) = \n",
    "    node.SelectNodes query\n",
    "\n",
    "let inline innerText (node : HtmlNode) = \n",
    "    node.InnerText\n",
    "\n",
    "let inline attr name (node : HtmlNode) = \n",
    "    node.GetAttributeValue(name, \"\")\n",
    "\n",
    "let inline hasAttr name value node = \n",
    "    attr name node = value\n",
    "    \n",
    "//Use AngleSharp to auto-fix broken documents; converts to HTML5 standard\n",
    "let AutoFixHtml (html : string) =\n",
    "    let parser = new AngleSharp.Parser.Html.HtmlParser();\n",
    "    let document = parser.Parse( html );\n",
    "    document.DocumentElement.OuterHtml\n",
    "\n",
    "let createDocAutoFix html =\n",
    "    let doc = new HtmlDocument()\n",
    "    html |> AutoFixHtml |> doc.LoadHtml\n",
    "    doc.DocumentNode\n",
    "\n",
    "//Extraction data from HTML\n",
    "type Class =\n",
    "    {\n",
    "        Title: string\n",
    "        Type: string\n",
    "        Time: string\n",
    "        StartDateTime: DateTime //hackish: date and time components have slightly different semantics\n",
    "        EndDateTime: DateTime //hackish: date and time components have slightly different semantics\n",
    "        Days: string[]\n",
    "        Where: string\n",
    "        DateRange: string\n",
    "        ScheduleType: string\n",
    "        Instructors: string\n",
    "    }\n",
    "\n",
    "let GetDays dayString = \n",
    "    if dayString = \"&nbsp;\" then\n",
    "        [||]\n",
    "    else \n",
    "        let result = new ResizeArray<string>()\n",
    "        for c in dayString do\n",
    "            match c with\n",
    "            | 'M' -> result.Add( \"MO\" )\n",
    "            | 'T' -> result.Add( \"TU\" )\n",
    "            | 'W' -> result.Add( \"WE\" )\n",
    "            | 'R' -> result.Add( \"TH\" )\n",
    "            | 'F' -> result.Add( \"FR\" )\n",
    "            | 'S' -> result.Add( \"SA\" )\n",
    "            | _ -> ()\n",
    "        //\n",
    "        result.ToArray()\n",
    "        \n",
    "let GetStartEndDateTime (dateNode:HtmlNode) (timeNode:HtmlNode) = \n",
    "    let dateSplit = dateNode.InnerHtml.Split('-')\n",
    "    let timeSplit = timeNode.InnerHtml.Split('-')\n",
    "    match dateSplit.Length,timeSplit.Length with\n",
    "    //we have valid dates and times\n",
    "    | 2,2 -> \n",
    "        let s = dateSplit.[0].Trim() + \" \" + timeSplit.[0].Trim() |> DateTime.Parse\n",
    "        let e = dateSplit.[1].Trim() + \" \" + timeSplit.[1].Trim() |> DateTime.Parse\n",
    "        s,e\n",
    "    //all other cases we can't put on calendar; we use MinValue instead of options\n",
    "    | 2,_ -> DateTime.MinValue,DateTime.MinValue \n",
    "    | _,2 -> DateTime.MinValue,DateTime.MinValue \n",
    "    | _,_ -> DateTime.MinValue,DateTime.MinValue\n",
    "\n",
    "//ICS related\n",
    "let GetVEvents (c : Class) =\n",
    "    let s = c.StartDateTime\n",
    "    let e = c.EndDateTime\n",
    "    //Fix the semantics: combine the time component of End with the date component of start to get the end time on a given day.\n",
    "    let dayEndTime = new DateTime(s.Year,s.Month,s.Day,e.Hour,e.Minute,e.Second)\n",
    "    seq {\n",
    "        for day in c.Days do\n",
    "            yield \"\"\"BEGIN:VEVENT\n",
    "DTSTART;TZID=America/Chicago:#START#\n",
    "DTEND;TZID=America/Chicago:#END#\n",
    "RRULE:FREQ=WEEKLY;UNTIL=#UNTILZ#;BYDAY=#DAY#\n",
    "DTSTAMP:#NOWZ#\n",
    "CREATED:#NOWZ#\n",
    "DESCRIPTION:\n",
    "LAST-MODIFIED:#NOWZ#\n",
    "LOCATION:#WHERE#\n",
    "SEQUENCE:0\n",
    "STATUS:CONFIRMED\n",
    "SUMMARY:#NAME#\n",
    "TRANSP:OPAQUE\n",
    "END:VEVENT\"\"\".Replace(\"#START#\",c.StartDateTime.ToString(\"yyyyMMddTHHmmss\")).Replace(\"#END#\",dayEndTime.ToString(\"yyyyMMddTHHmmss\")).Replace(\"#UNTILZ#\",c.EndDateTime.ToUniversalTime().ToString(\"yyyyMMddTHHmmssZ\")).Replace(\"#DAY#\",day).Replace(\"#NOWZ#\",DateTime.Now.ToUniversalTime().ToString(\"yyyyMMddTHHmmssZ\")).Replace(\"#NAME#\",c.Title).Replace(\"#WHERE#\",c.Where)\n",
    "    }\n",
    "\n",
    "// Looked at https://github.com/UHCalendarTeam/ANtICalendar but it wasn't good\n",
    "// Instead made a calendar template on Google and reverse engineered it\n",
    "let WriteVEventsToFile filename vEvents = \n",
    "    let prefix = \"\"\"BEGIN:VCALENDAR\n",
    "    PRODID:-//Google Inc//Google Calendar 70.9054//EN\n",
    "    VERSION:2.0\n",
    "    CALSCALE:GREGORIAN\n",
    "    METHOD:PUBLISH\n",
    "    X-WR-CALNAME:#NAME#\n",
    "    X-WR-TIMEZONE:America/Chicago\n",
    "    BEGIN:VTIMEZONE\n",
    "    TZID:America/Chicago\n",
    "    X-LIC-LOCATION:America/Chicago\n",
    "    BEGIN:DAYLIGHT\n",
    "    TZOFFSETFROM:-0600\n",
    "    TZOFFSETTO:-0500\n",
    "    TZNAME:CDT\n",
    "    DTSTART:19700308T020000\n",
    "    RRULE:FREQ=YEARLY;BYMONTH=3;BYDAY=2SU\n",
    "    END:DAYLIGHT\n",
    "    BEGIN:STANDARD\n",
    "    TZOFFSETFROM:-0500\n",
    "    TZOFFSETTO:-0600\n",
    "    TZNAME:CST\n",
    "    DTSTART:19701101T020000\n",
    "    RRULE:FREQ=YEARLY;BYMONTH=11;BYDAY=1SU\n",
    "    END:STANDARD\n",
    "    END:VTIMEZONE\"\"\".Replace(\"#NAME#\",filename).Replace(\"    \",\"\")\n",
    "    let icsString = prefix + \"\\n\" + (vEvents |> String.concat \"\\n\") + \"\\n\" + \"END:VCALENDAR\"\n",
    "    System.IO.File.WriteAllText(filename + \".ics\", icsString)\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Department Calendar View\n",
    "\n",
    "Run the query to get all courses in a department, extract the time/date information for each course, then export this information in ICS format.\n",
    "ICS can then be loaded into your favorite calendar app, e.g. Google Calendar. \n",
    "It is recommended to load each department ICS as its own calendar (rather than merging ICS into a single calendar) to retain color coding for departments."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 34,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\"DONE\""
      ]
     },
     "execution_count": 34,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "//Banner Term, e.g. 201680 for Fall 2016; 201610 for Spring\n",
    "let term = \"201910\"\n",
    "let dept = \"PHIL\"\n",
    "\n",
    "let classEvents =\n",
    "    QueryDepartment dept term\n",
    "    |> createDocAutoFix\n",
    "    //Auto fix changes source html to HTML5 standard, so XPath must use e.g. Chrome inspector structure and not source structure\n",
    "    |> selectNodes \"html/body/div/table/tbody/tr/th/a | html/body/div/table/tbody/tr/td/table\"\n",
    "    //|> Seq.map( fun x -> x.InnerHtml) //for debug\n",
    "    |> Seq.chunkBySize 2 //first is title; second is table of time/location/etc\n",
    "    |> Seq.collect( fun courseChunk -> \n",
    "        //Need distinction b/w course and class chunks b/c Psychology has GenEd with variable classes across the week; see previous commit for simpler approach\n",
    "        courseChunk.[1] \n",
    "        |> selectNodes \"descendant::td\" \n",
    "        |> Seq.chunkBySize 7\n",
    "        |> Seq.map( fun classChunk -> \n",
    "            let days = GetDays classChunk.[2].InnerHtml\n",
    "            let start,stop = GetStartEndDateTime classChunk.[4] classChunk.[1]\n",
    "            {\n",
    "                Title=courseChunk.[0].InnerHtml;\n",
    "                Type=classChunk.[0].InnerHtml;\n",
    "                Time=classChunk.[1].InnerHtml;\n",
    "                StartDateTime =  start\n",
    "                EndDateTime = stop\n",
    "                Days=days;\n",
    "                Where=classChunk.[3].InnerHtml;\n",
    "                DateRange=classChunk.[4].InnerHtml;\n",
    "                ScheduleType=classChunk.[5].InnerHtml;\n",
    "                Instructors=classChunk.[6].InnerText;\n",
    "                }\n",
    "            )\n",
    "      )\n",
    "    //Purge events without times, e.g. dissertation and independent study\n",
    "    |> Seq.filter (fun x -> x.StartDateTime <> DateTime.MinValue)\n",
    "    |> Seq.collect GetVEvents\n",
    "    |> Seq.toList\n",
    "    \n",
    "WriteVEventsToFile dept classEvents\n",
    "\n",
    "\"DONE\""
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Class Calendar View\n",
    "\n",
    "Given a list of courses, extract the time/date information for each course, then export this information in ICS format.\n",
    "\n",
    "ICS can then be loaded into your favorite calendar app, e.g. Google Calendar. \n",
    "\n",
    "## Course List\n",
    "\n",
    "The list of courses below was generated in a spreadsheet saved as tab delimited. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 37,
   "metadata": {},
   "outputs": [],
   "source": [
    "let courseList = \"\"\"degree\tdepartment\tnumber\n",
    "MINOR\tPSYC\t1030\n",
    "MINOR\tPSYC\t3303\n",
    "MINOR\tPSYC\t3530\n",
    "MINOR\tCOMP\t1900\n",
    "MINOR\tPHIL\t3460\n",
    "MINOR\tPHIL\t4421\n",
    "MINOR\tPHIL\t4661\n",
    "MINOR\tECON\t4512\n",
    "MINOR\tCOMP\t2700\n",
    "MINOR\tCOMP\t4720\n",
    "MINOR\tMATH\t2120\n",
    "MINOR\tMATH\t4083\n",
    "MINOR\tENGL\t3511\"\"\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 39,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\"DONE\""
      ]
     },
     "execution_count": 39,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "let term = \"201910\"\n",
    "let calendarName = \"minor\"\n",
    "\n",
    "let classEvents = \n",
    "    courseList.Split(\"\\n\")\n",
    "    |> Seq.skip 1 //skip header\n",
    "    |> Seq.choose( fun x -> \n",
    "        let s = x.Split('\\t')\n",
    "        let degree = s.[0]\n",
    "        let dept = s.[1]\n",
    "        let id = s.[2]\n",
    "        //handle queries that fail\n",
    "        match QueryClass dept id term with\n",
    "        | true, result -> Some( result )\n",
    "        | false, _ -> None\n",
    "        )\n",
    "    |> Seq.map createDocAutoFix\n",
    "    //|> Seq.map( fun x -> x.InnerHtml) //for debug\n",
    "    |> Seq.collect( fun html -> \n",
    "        html\n",
    "        //Auto fix changes source html to HTML5 standard, so XPath must use e.g. Chrome inspector structure and not source structure\n",
    "        |> selectNodes \"html/body/div/table/tbody/tr/th/a | html/body/div/table/tbody/tr/td/table\"\n",
    "        //|> Seq.map( fun x -> x.InnerHtml) //for debug\n",
    "        |> Seq.chunkBySize 2 //first is title; second is table of time/location/etc\n",
    "        |> Seq.collect( fun courseChunk -> \n",
    "            //Need distinction b/w course and class chunks b/c Psychology has GenEd with variable classes across the week; see previous commit for simpler approach\n",
    "            courseChunk.[1] \n",
    "            |> selectNodes \"descendant::td\" \n",
    "            |> Seq.chunkBySize 7\n",
    "            |> Seq.map( fun classChunk -> \n",
    "                let days = GetDays classChunk.[2].InnerHtml\n",
    "                let start,stop = GetStartEndDateTime classChunk.[4] classChunk.[1]\n",
    "                {\n",
    "                    Title=courseChunk.[0].InnerHtml;\n",
    "                    Type=classChunk.[0].InnerHtml;\n",
    "                    Time=classChunk.[1].InnerHtml;\n",
    "                    StartDateTime =  start\n",
    "                    EndDateTime = stop\n",
    "                    Days=days;\n",
    "                    Where=classChunk.[3].InnerHtml;\n",
    "                    DateRange=classChunk.[4].InnerHtml;\n",
    "                    ScheduleType=classChunk.[5].InnerHtml;\n",
    "                    Instructors=classChunk.[6].InnerText;\n",
    "                    }\n",
    "                )\n",
    "          )\n",
    "    )\n",
    "    //Purge events without times, e.g. dissertation and independent study\n",
    "    |> Seq.filter (fun x -> x.StartDateTime <> DateTime.MinValue)\n",
    "    |> Seq.collect GetVEvents\n",
    "    |> Seq.toList\n",
    "    \n",
    "WriteVEventsToFile calendarName classEvents\n",
    "\"DONE\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "F#",
   "language": "fsharp",
   "name": "ifsharp"
  },
  "language_info": {
   "codemirror_mode": "",
   "file_extension": ".fs",
   "mimetype": "text/x-fsharp",
   "name": "fsharp",
   "nbconvert_exporter": "",
   "pygments_lexer": "",
   "version": "4.3.1.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
