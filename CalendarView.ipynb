{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Calendar View\n",
    "\n",
    "Given a department code and a term, creates a calendar view of scheduled courses in Banner for the purpose of visualizing course scheduling conflicts.\n",
    "\n",
    "The primary use case for this is determining where to schedule a new IIS class so as to minimize conflicts with other departments, but it would probably be equally useful for a department to visualize its own schedule.\n",
    "\n",
    "Note that you will need to install and reference NuGet packages as appropriate on your system.\n",
    "\n",
    "## Basic Banner Query and HTML walking\n",
    "\n",
    "The code below describes a basic Banner POST for all courses offered by a department as well as some methods for processing the html"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 53,
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "Possible incorrect indentation: this token is offside of context started at position (8:18). Try indenting this token further or using standard formatting conventions.\n",
      "Possible incorrect indentation: this token is offside of context started at position (8:18). Try indenting this token further or using standard formatting conventions.\n",
      "Possible incorrect indentation: this token is offside of context started at position (8:18). Try indenting this token further or using standard formatting conventions.\n",
      "Possible incorrect indentation: this token is offside of context started at position (8:18). Try indenting this token further or using standard formatting conventions."
     ]
    }
   ],
   "source": [
    "#r \"/z/aolney/repos/FSharp.Data.2.3.2/lib/net40/FSharp.Data.dll\"\n",
    "#r \"/z/aolney/repos/HtmlAgilityPack.1.4.9.5/lib/Net40/HtmlAgilityPack.dll\"\n",
    "open FSharp.Data\n",
    "open HtmlAgilityPack\n",
    "open System\n",
    "\n",
    "let QueryDepartment (dept:string) (term:string) =\n",
    "    let result = Http.RequestString( \n",
    "        \"https://banssbprod.memphis.edu/pls/PROD/bwckschd.p_get_crse_unsec\",\n",
    "        query=[\n",
    "            \"term_in\", term.Trim();\n",
    "            \"sel_subj\",\"dummy\";\n",
    "            \"sel_day\",\"dummy\";\n",
    "            \"sel_schd\",\"dummy\";\n",
    "            \"sel_insm\",\"dummy\";\n",
    "            \"sel_camp\",\"dummy\";\n",
    "            \"sel_levl\",\"dummy\";\n",
    "            \"sel_sess\",\"dummy\";\n",
    "            \"sel_instr\",\"dummy\";\n",
    "            \"sel_ptrm\",\"dummy\";\n",
    "            \"sel_attr\",\"dummy\";\n",
    "            \"sel_subj\",dept.Trim();\n",
    "            \"sel_crse\",\"\"; //number.Trim();\n",
    "            \"sel_title\",\"\";\n",
    "            \"sel_insm\",\"%\";\n",
    "            \"sel_from_cred\",\"\";\n",
    "            \"sel_to_cred\",\"\";\n",
    "            \"sel_camp\",\"%\";\n",
    "            \"sel_levl\",\"%\";\n",
    "            \"sel_ptrm\",\"%\";\n",
    "            \"sel_instr\",\"%\";\n",
    "            \"sel_attr\",\"%\";\n",
    "            \"begin_hh\",\"0\";\n",
    "            \"begin_mi\",\"0\";\n",
    "            \"begin_ap\",\"a\";\n",
    "            \"end_hh\",\"0\";\n",
    "            \"end_mi\",\"0\";\n",
    "            \"end_ap\",\"a\" ], \n",
    "        httpMethod=\"POST\")\n",
    "    //\n",
    "    result\n",
    "\n",
    "//Creates a doc from html string\n",
    "let createDoc html =\n",
    "    let doc = new HtmlDocument()\n",
    "    doc.LoadHtml html\n",
    "    doc.DocumentNode\n",
    "\n",
    "//A simple version of the more complex https://stackoverflow.com/questions/22661640/how-to-fix-ill-formed-html-with-html-agility-pack\n",
    "//However, it appears htmlagilitypack does not detect errors very well\n",
    "let createDocAutoFix html =\n",
    "    let doc = new HtmlDocument()\n",
    "    doc.LoadHtml html\n",
    "    let tempHtml = html.Split( [|\"\\n\";\"\\r\\n\"|], StringSplitOptions.None ) //we need line numbers\n",
    "    if doc.ParseErrors <> null then\n",
    "        for error in doc.ParseErrors do\n",
    "            if error.Code = HtmlParseErrorCode.TagNotOpened || error.Code = HtmlParseErrorCode.TagNotClosed then\n",
    "                tempHtml.[error.Line] <- tempHtml.[error.Line].Remove(error.LinePosition, error.SourceText.Length)\n",
    "    doc.LoadHtml (tempHtml |> String.concat \"\\n\")\n",
    "    doc.DocumentNode\n",
    "    \n",
    "let descendants (name : string) (node : HtmlNode) = \n",
    "    node.Descendants name\n",
    "    \n",
    "let selectNodes (query : string) (node : HtmlNode) = \n",
    "    node.SelectNodes query\n",
    "\n",
    "let inline innerText (node : HtmlNode) = \n",
    "    node.InnerText\n",
    "\n",
    "let inline attr name (node : HtmlNode) = \n",
    "    node.GetAttributeValue(name, \"\")\n",
    "\n",
    "let inline hasAttr name value node = \n",
    "    attr name node = value"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Calendar View\n",
    "\n",
    "Run the query to get all courses in a department, extract the time/date information for each course, then export this information in ICS format."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 57,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\"DONE\""
      ]
     },
     "execution_count": 57,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "//Banner Term, e.g. 201680 for Fall 2016; 201610 for Spring\n",
    "let term = \"201910\"\n",
    "let dept = \"COMP\"\n",
    "\n",
    "type Class =\n",
    "    {\n",
    "        Title: string\n",
    "        Type: string\n",
    "        Time: string\n",
    "        StartDateTime: DateTime //hackish: date and time components have slightly different semantics\n",
    "        EndDateTime: DateTime //hackish: date and time components have slightly different semantics\n",
    "        Days: string[]\n",
    "        Where: string\n",
    "        DateRange: string\n",
    "        ScheduleType: string\n",
    "        Instructors: string\n",
    "    }\n",
    "\n",
    "let GetVEvents (c : Class) =\n",
    "    let s = c.StartDateTime\n",
    "    let e = c.EndDateTime\n",
    "    let dayEndTime = new DateTime(s.Year,s.Month,s.Day,e.Hour,e.Minute,e.Second)\n",
    "    seq {\n",
    "        for day in c.Days do\n",
    "            yield \"\"\"BEGIN:VEVENT\n",
    "DTSTART;TZID=America/Chicago:#START#\n",
    "DTEND;TZID=America/Chicago:#END#\n",
    "RRULE:FREQ=WEEKLY;UNTIL=#UNTILZ#;BYDAY=#DAY#\n",
    "DTSTAMP:#NOWZ#\n",
    "CREATED:#NOWZ#\n",
    "DESCRIPTION:\n",
    "LAST-MODIFIED:#NOWZ#\n",
    "LOCATION:\n",
    "SEQUENCE:0\n",
    "STATUS:CONFIRMED\n",
    "SUMMARY:#NAME#\n",
    "TRANSP:OPAQUE\n",
    "END:VEVENT\"\"\".Replace(\"#START#\",c.StartDateTime.ToString(\"yyyyMMddTHHmmss\")).Replace(\"#END#\",dayEndTime.ToString(\"yyyyMMddTHHmmss\")).Replace(\"#UNTILZ#\",c.EndDateTime.ToUniversalTime().ToString(\"yyyyMMddTHHmmssZ\")).Replace(\"#DAY#\",day).Replace(\"#NOWZ#\",DateTime.Now.ToUniversalTime().ToString(\"yyyyMMddTHHmmssZ\")).Replace(\"#NAME#\",c.Title)\n",
    "    }\n",
    "\n",
    "let GetDays dayString = \n",
    "    if dayString = \"&nbsp;\" then\n",
    "        [||]\n",
    "    else \n",
    "        //originally used System.DayOfWeek, but storing as ical is more useful\n",
    "        let result = new ResizeArray<string>()\n",
    "        for c in dayString do\n",
    "            match c with\n",
    "            | 'M' -> result.Add( \"MO\" )//DayOfWeek.Monday )\n",
    "            | 'T' -> result.Add( \"TU\" ) //DayOfWeek.Tuesday )\n",
    "            | 'W' -> result.Add( \"WE\" ) //DayOfWeek.Wednesday )\n",
    "            | 'R' -> result.Add( \"TH\" ) //DayOfWeek.Thursday )\n",
    "            | 'F' -> result.Add( \"FR\" ) //DayOfWeek.Friday )\n",
    "            | 'S' -> result.Add( \"SA\" ) //DayOfWeek.Saturday )\n",
    "            | _ -> ()\n",
    "        //\n",
    "        result.ToArray()\n",
    "        \n",
    "let GetStartEndDateTime (dateNode:HtmlNode) (timeNode:HtmlNode) = \n",
    "    let dateSplit = dateNode.InnerHtml.Split('-')\n",
    "    let timeSplit = timeNode.InnerHtml.Split('-')\n",
    "    match dateSplit.Length,timeSplit.Length with\n",
    "    //we have valid dates and times\n",
    "    | 2,2 -> \n",
    "        let s = dateSplit.[0].Trim() + \" \" + timeSplit.[0].Trim() |> DateTime.Parse\n",
    "        let e = dateSplit.[1].Trim() + \" \" + timeSplit.[1].Trim() |> DateTime.Parse\n",
    "        s,e\n",
    "    //all other cases we can't put on calendar; we use MinValue instead of options\n",
    "    | 2,_ -> DateTime.MinValue,DateTime.MinValue \n",
    "    | _,2 -> DateTime.MinValue,DateTime.MinValue \n",
    "    | _,_ -> DateTime.MinValue,DateTime.MinValue\n",
    "\n",
    "    \n",
    "let classEvents =\n",
    "    QueryDepartment dept term\n",
    "    |> createDoc\n",
    "    |> selectNodes \"html/body/div/table/tr/th/a | html/body/div/table/tr/td/table/tr/td\"\n",
    "    //|> Seq.map( fun x -> x.InnerHtml)\n",
    "    |> Seq.chunkBySize 8 //first is title; next 7 is table of info\n",
    "    //|> Seq.take 19 //for debug\n",
    "    |> Seq.map( fun classChunk -> \n",
    "        let days = GetDays classChunk.[3].InnerHtml\n",
    "        let start,stop = GetStartEndDateTime classChunk.[5] classChunk.[2]\n",
    "        {\n",
    "            Title=classChunk.[0].InnerHtml;\n",
    "            Type=classChunk.[1].InnerHtml;\n",
    "            Time=classChunk.[2].InnerHtml;\n",
    "            StartDateTime =  start\n",
    "            EndDateTime = stop\n",
    "            Days=days;\n",
    "            Where=classChunk.[4].InnerHtml;\n",
    "            DateRange=classChunk.[5].InnerHtml;\n",
    "            ScheduleType=classChunk.[6].InnerHtml;\n",
    "            Instructors=classChunk.[7].InnerText;\n",
    "            }\n",
    "      )\n",
    "    //Purge events without times\n",
    "    |> Seq.filter (fun x -> x.StartDateTime <> DateTime.MinValue)\n",
    "    |> Seq.collect GetVEvents\n",
    "    |> Seq.toList\n",
    "\n",
    "//Make ICS\n",
    "// Looked at https://github.com/UHCalendarTeam/ANtICalendar but it wasn't good\n",
    "// Instead made a calendar template on Google and reverse engineered it\n",
    "let prefix = \"\"\"BEGIN:VCALENDAR\n",
    "PRODID:-//Google Inc//Google Calendar 70.9054//EN\n",
    "VERSION:2.0\n",
    "CALSCALE:GREGORIAN\n",
    "METHOD:PUBLISH\n",
    "X-WR-CALNAME:#NAME#\n",
    "X-WR-TIMEZONE:America/Chicago\n",
    "BEGIN:VTIMEZONE\n",
    "TZID:America/Chicago\n",
    "X-LIC-LOCATION:America/Chicago\n",
    "BEGIN:DAYLIGHT\n",
    "TZOFFSETFROM:-0600\n",
    "TZOFFSETTO:-0500\n",
    "TZNAME:CDT\n",
    "DTSTART:19700308T020000\n",
    "RRULE:FREQ=YEARLY;BYMONTH=3;BYDAY=2SU\n",
    "END:DAYLIGHT\n",
    "BEGIN:STANDARD\n",
    "TZOFFSETFROM:-0500\n",
    "TZOFFSETTO:-0600\n",
    "TZNAME:CST\n",
    "DTSTART:19701101T020000\n",
    "RRULE:FREQ=YEARLY;BYMONTH=11;BYDAY=1SU\n",
    "END:STANDARD\n",
    "END:VTIMEZONE\"\"\".Replace(\"#NAME#\",dept)\n",
    "\n",
    "\n",
    "let icsString = prefix + \"\\n\" + (classEvents |> String.concat \"\\n\") + \"\\n\" + \"END:VCALENDAR\"\n",
    "System.IO.File.WriteAllText(dept + \".ics\", icsString)\n",
    "\"DONE\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "F#",
   "language": "fsharp",
   "name": "ifsharp"
  },
  "language_info": {
   "codemirror_mode": "",
   "file_extension": ".fs",
   "mimetype": "text/x-fsharp",
   "name": "fsharp",
   "nbconvert_exporter": "",
   "pygments_lexer": "",
   "version": "4.3.1.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
